plugins {
    id 'application'
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
}

group = 'com.glencoesoftware'
version = '0.2.5-SNAPSHOT'

mainClassName = 'com.glencoesoftware.bioformats2raw.Converter'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url 'https://artifacts.openmicroscopy.org/artifactory/repo/'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/n5-zarr-snapshots/'
    }
    maven {
        url 'https://maven.scijava.org/content/groups/public'
    }
}

def bfVersion = '6.4.0'
dependencies {
    implementation "ome:formats-gpl:${bfVersion}"
    implementation 'info.picocli:picocli:4.2.0'
    implementation 'com.univocity:univocity-parsers:2.8.4'
    implementation 'org.janelia.saalfeldlab:n5:2.2.0'
    implementation 'org.janelia.saalfeldlab:n5-blosc:1.1.0'
    implementation 'org.janelia.saalfeldlab:n5-zarr:0.0.4'

    // https://github.com/junit-team/junit5-samples/blob/master/junit5-migration-gradle/build.gradle
    def junit4Version        = '4.12'
    def junitVintageVersion  = '5.2.0'
    def junitJupiterVersion  = '5.2.0'
    def junitPlatformVersion = '1.2.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "junit:junit:${junit4Version}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitVintageVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"

    // https://stackoverflow.com/questions/45462987/junit5-with-intellij-and-gradle
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
}

task generateSources {
    def outputDir = file("$buildDir/resources/properties/com/glencoesoftware/bioformats2raw")
    outputs.dir outputDir
    doFirst {
        def srcFile = new File(outputDir, "version.properties")
        srcFile.parentFile.mkdirs()
        srcFile.write("""Version=$project.version
Bio-Formats version=${bfVersion}""")
    }
}
compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

sourceSets {
    main {
        resources {
            srcDirs "$buildDir/resources/properties"
        }
    }
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
                include 'LICENSE.txt'
            }
        }
    }
}

publishing {
    publications {
        bioformats2raw(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}

checkstyle {
    toolVersion = "8.26"
}
