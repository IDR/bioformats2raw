plugins {
    id 'application'
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
}

group = 'com.glencoesoftware'
version = '0.4.1-SNAPSHOT'

mainClassName = 'com.glencoesoftware.bioformats2raw.Converter'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/n5-zarr-snapshots/'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/jzarr-snapshots'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.external'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/scijava-thirdparty'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/jzarr-releases'
    }
    maven {
        name 'Unidata'
        url 'https://artifacts.glencoesoftware.com/artifactory/unidata-releases'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'edu.ucar', 'module': 'cdm'
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.10.0'
    implementation 'ome:formats-gpl:6.9.1'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'com.univocity:univocity-parsers:2.8.4'
    implementation 'com.bc.zarr:jzarr:0.3.5'
    // implementation 'org.carlspring.cloud.aws:s3fs-nio:1.0-SNAPSHOT'
    // implementation 'io.nextflow:nxf-s3fs:1.1.0'
    implementation 'org.lasersonlab:s3fs:2.2.3'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'


    implementation 'org.openpnp:opencv:3.4.2-1'
    implementation 'me.tongfei:progressbar:0.9.0'

    // https://github.com/junit-team/junit5-samples/blob/master/junit5-migration-gradle/build.gradle
    def junitVintageVersion  = '5.7.0'
    def junitJupiterVersion  = '5.7.0'
    def junitPlatformVersion = '1.7.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"

    // https://stackoverflow.com/questions/45462987/junit5-with-intellij-and-gradle
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Implementation-Version": archiveVersion
        )
    }
}

test {
    useJUnitPlatform()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
                include 'LICENSE.txt'
            }
        }
    }
}

publishing {
    publications {
        bioformats2raw(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}

checkstyle {
    toolVersion = "8.26"
}
